#!/bin/tcsh
#$ -S /bin/tcsh
source ./code/code.main/custom-tcshrc     # shell settings

##
## USAGE: scripts-qsub-wrapper THREADS[,MEMORY] SCRIPT-PATH OUTPUT-DIR ARGS ...
##

if ($#argv < 3) then
  grep '^##' $0
  exit
endif

set resources = $1
set prog = `which $2`
set out = $3
set jobname = `basename $3`

shift
shift

if (-e $out) then
  scripts-send2err "[scripts-qsub-wrapper] Directory \'$out\' exists, skipping..."
  exit 1
endif

set nthreads = `echo $resources,, | cut -d',' -f1`                # number of threads
set mem = `echo $resources,, | cut -d',' -f2`                     # memory, e.g. 10G
set rtime = `echo $resources,, | cut -d',' -f3`                   # number of hours, e.g. 24
if ($rtime == '') then
  set rtime = 24
endif

scripts-send2err "[scripts-qsub-wrapper] Generating results in directory \'$out\'..."
if (-e $out) rm -rf $out
scripts-create-path $out/ 
set pref = $out/job

echo '#\!/bin/tcsh' >! $pref.sh
echo '#'SBATCH -J $jobname >> $pref.sh
echo '#'SBATCH --time=0-${rtime}:00:00 >> $pref.sh
echo '#'SBATCH -n 1 >> $pref.sh
echo '#'SBATCH -c $nthreads >> $pref.sh
echo '#'SBATCH -o $pref.out >> $pref.sh
echo '#'SBATCH -e $pref.err >> $pref.sh

if ("$mem" == "") then
  scripts-print-cmdline $prog $argv:q >> $pref.sh
  sbatch ./$pref.sh >! $pref.id

else
  echo '#'SBATCH --mem=$mem >> $pref.sh
  scripts-print-cmdline $prog $argv:q >> $pref.sh
  sbatch ./$pref.sh >! $pref.id

endif  

cat $pref.id | cut -d' ' -f4  


